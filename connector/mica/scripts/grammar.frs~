//load_module("unsw.cse.framescript.GUI");;

_HELLO_ :: { hello | hi | hi there | hey | sup | hay};;
_DRINK_ :: { drink | water | coke | lemonoade | vodka | gin and tonic };;
_ORDER_ :: { to buy | to order | get | bring me | want | order | bring | have };;
_YES_   :: { yes | yep | yeah | yay };;
_NO_    :: { no | nope | nah | nay | no thanks | no thank you};;
_MOVE_  :: { go | move | hurry to | get to };;
_LOCATION_ :: {Desk A | Desk B | Desk C | Door | No where | over there | over | there | here | hay};;
_MANNER_ :: {please | can I | can You | excuse me | thank you | };;
_IM_ :: {i'm | i am | we | me | im};;
_SIT_ :: { at | sitting at | located | sitting | here at | here | where};;
_FAVOURITE_ :: { usual | last order | another one | favourite};;
_FINISH_ :: {no | nope | nah | nay | no thanks | no thank you | that is all | that's all | no you | finish};;
	

STORED_LOCATION;;
			
main ::
	domain order
	trigger { * {<_MOVE_> | <_LOCATION_>} *  } 
	
	Bring me the glass ==>
	[
		Bringing you the glass
	]
	
	* <_SIT_> * <_LOCATION_> * ==>
	[
		:setLocation ^4 
	]
	
	<_IM_> *  ==>
	[
		I don't know where ^2 is.
	]
	
	 * <_MOVE_> * <_LOCATION_> * ==>
	[
		:goLocation: ^4
	]
;;


confirmfinish ::
	domain loc
	trigger { confirmfinish }
	
	* <_YES_> * ==>
	[
		Ok, where should I bring these to?
		#goto(location)
	]
	
	* { <_YES_> | get } * ==>
	[
		Ok, where should I bring these to?
		#goto(location)
	]
	
	* <_NO_> * ==>
	[
		I must of misheard, what did else did you want?
		#goto(making)	
	]
;;

making ::
	domain order
	trigger { * { <_ORDER_> | <_DRINK_> } *}
	neworder ==>
	[
		Hi, what can I get you?
	]
	* <_ORDER_> * <_DRINK_> *==>
	[
		You wanted a ^4?
		#goto(confirmorder)
	]
	
	* <_DRINK_> *==>
	[
		You wanted a ^2?
		#goto(confirmorder)
	]
	
	* <_ORDER_> * <_FAVOURITE_> * ==>
	[
		#goto(knowfavourites, ^0)
	]
		
	{Excuse me | Can you | Can I} { can I | can you | please | } <_ORDER_> * { please | pretty please | thank you} ==>
	[
		I don't know what ^3 is. 
		#goto(confused, conf)
	]
	
	{I want | I | } {not <_DRINK_>} ==>
	[
		I don't know what ^2 is. 
		#goto(confused, conf)
	]
	
	* <_ORDER_> * ==>
	[
		I don't know what ^3 is. 
		#goto(confused, conf)
	]
	
	* <_NO_> * ==>
	[
		Where should I bring these to?
		#goto(location)
	]
;;


confused ::
	domain order
	trigger { confusion }
	conf ==> 
	[
		What did you want to order again? 
	]
	* <_DRINK_> * ==>
	[
		:I see you meant ^2. And where did you want it?
		#goto(location)
	]
	* <_FAVOURITE_> * ==>
	[
		I see you meant your favourite.
		#goto(knowfavourites, ^0)
	]	
	*  ==>
	[
		I still don't recognise ^0
	]
	
;;


knowfavourites ::
	domain fav
	trigger {* <_FAVOURITE_> *}

	
	* <_FAVOURITE_> <_DRINK_> * ==> 
	[
		:setFavourite ^2
		#goto(getfavourite)
	]
	
	* <_ORDER_> my <_FAVOURITE_> * ==>
	[
		:getFavourite known
		#goto(location)
	]
	
	* <_FAVOURITE_>  * ==> 
	[
		What did you want me to do with your favourite drink?
	]
	
	* ==>
	[
		What did you want me to do with your favourite drink?
	]
	
;;

setfavourite ::
	domain fav
	trigger { setfavourite }
	* <_DRINK_> * ==>
	[
		:setFavourite ^2
		goto(making)
	]
	* ==>
	[
		No favourite? What drink did you want a drink anyway?
	]
;;

confirmorder::
	domain order
	trigger {confirm}
	confirm ==>
	[
		So you want me to bring you the drink?
	]
	
	* { <_YES_> } * ==>
	[
		Ok, I'll bring that with me. Anything else?
		#goto(making)
	]
	
	* <_NO_> * ==>
	[
		I must of misheard, what did you want to do again?
		#previous_topic()
		
	]
	//* ==>
	//[::confirmorder]
;;

getfavourite ::
	domain fav
	trigger {getfavourite}
	shouldiget ==>
	[
		Did you want me to get that for you?
	]
	
	* <_YES_> * ==>
	[
		Ok, where do you want me to bring it to.
		#goto(location)
	]

	* <_NO_> * ==>
	[
		Ok, nevermind then.
		#goto(making)
	]
	* ==>
	[	#goto(favourites,^0)
		::favourite
	]
;;


location::
	domain loc
	trigger {* <_LOCATION_> *}
	
	//on_entry hello there
	
	* <_LOCATION_> * ==>
	[
		:confirmLocation ^2
		#goto(confirmlocation)
		#(STORED_LOCATION = ^2)
	]
	
	* <_NO_> * ==>
	[
		My bad, I thought that was all you want?
		#goto(making)
	]
	
	* ==>
	[
		Where did you mean by ^0 ::location
	]
	
;;	

confirmlocation::
	domain loc
	trigger {confirmlocation}
	* <_YES_> * ==>
	[
		: confirmLocation #defined(STORED_LOCATION)
		#goto(making)
	]

	* <_NO_> * ==>
	[
		Oh did you mean another location?
		#goto(location)
	]
	
	* <_LOCATION_> * ==>
	[
		It's at ^2?
	]
	
	* ==>
	[
		What? confirmlocation()
	]
;;
greeting::
	domain demo
	trigger { * {<_HELLO_>} *  } 
		
	* <_HELLO_> * ==>
	[ 
		Hello. How are you?
		#goto(making)
	]
	
		
	* bye * ==>
	[
		Good bye.		
		#goto(making)
	]
	
	{I'm | I am }{ bad | not { good | well } } * ==>
	[ 
		I'm sorry to hear that, did you want a drink to cheer you up?		
		#goto(making) 
	]
		
	* { good | fine | well } * ==>
	[ 
		Well, that's good to hear. Celebrate it with a drink? 		
		#goto(making)
	]
;;

unsure::
	* ==>
	[
		Failsafe:I don't understand you by ^0
	]
;;

failsafe(demo, unsure);;

//script_browser();;
//frame_browser();;
